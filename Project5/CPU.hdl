// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:

    // handle A-register
    // choose between constant (A-instr) or ALU output (if C-instr) for input to A-register
    Mux16(a=instruction, b=outAlu, sel=instruction[15], out=inA);
    // check if load bit for A-register should be asserted
    Not(in=instruction[15], out=isAInstr); // 1 if A-instruction, 0 if C-instruction
    Or(a=isAInstr, b=instruction[5], out=loadA); // A-instr OR d1 is set
    ARegister(in=inA, load=loadA, out=outA, out[0..14]=addressM);

    // handle D-register
    // check if load bit for D-register should be asserted
    And(a=instruction[4], b=instruction[15], out=loadD); // d2 set AND C-instr
    DRegister(in=outAlu, load=loadD, out=outD);

    // set ALU's y-input to either A or M = RAM[A]
    Mux16(a=outA, b=inM, sel=instruction[12], out=aOrM);

    // handle pins for ALU
    ALU(x=outD, y=aOrM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], zr=zr, ng=ng, out=outAlu, out=outM);

    // check if ALU's computed value is positive
    Not(in=zr, out=notzr);
    Not(in=ng, out=notng);
    And(a=notzr, b=notng, out=pos);

    // check for jump
    And(a=instruction[2], b=ng, out=negjump); // jump if negative
    And(a=instruction[1], b=zr, out=zerjump); // jump if zero
    And(a=instruction[0], b=pos, out=posjump); // jump if positive

    Or(a=negjump, b=zerjump, out=jump1);
    Or(a=jump1, b=posjump, out=jump); // jump=1 if any jump conditions met

    And(a=jump, b=instruction[15], out=loadpc); // condition for jump met AND C-instruction

    // reset, load, or increment PC
    PC(in=outA, load=loadpc, inc=true, reset=reset, out[0..14]=pc);

    // check if writeM is asserted
    And(a=instruction[15], b=instruction[3], out=writeM); // C-instr and d3 is set
}