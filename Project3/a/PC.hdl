// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // if-else statements are evaluated backwards

    // if (inc[t] == 1) then out[t+1] = out[t] + 1
    Inc16(in=incin, out=addone);
    Mux16(a=incin, b=addone, sel=inc, out=incout);

    // if (load[t] == 1) then out[t+1] = in[t]
    Mux16(a=incout, b=in, sel=load, out=loadout);

    // if (reset[t] == 1) then out[t+1] = 0
    Mux16(a=loadout, b=false, sel=reset, out=resetout);

    Register(in=resetout, load=true, out=incin, out=out);
}
